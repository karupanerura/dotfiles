#!/usr/bin/perl
use strict;
use warnings;
use utf8;
use 5.14.0;
use File::Spec;
use URI;
use URI::Escape qw/uri_escape/;

my $target_branch = shift @ARGV || 'master';

sub slurp {
    my $file = shift;
    open my $fh, '<:encoding(utf-8)', $file;
    local $/;
    return <$fh>;
}

sub hosts {
    my @url = map s/\s+$//sr, <DATA>;
    my $local = File::Spec->catfile(glob('~'), '.pull-req-able.local');
    push @url => split /\n/, slurp($local) if -f $local;
    return @url;
}

sub remote {
    chomp(my $remote = `git config --get remote.origin.url`);
    return URI->new($remote);
}

sub current_branch {
    chomp(my $branch = `git rev-parse --abbrev-ref HEAD`);
    return $branch;
}

my $remote = remote();
$remote = URI->new('ssh://'.($remote =~ s!:!/!r)) unless defined $remote->scheme;
if (grep { $remote->host eq $_ } hosts()) {
    my $url = sprintf 'http://%s%s/compare/%s...%s', $remote->host, $remote->path =~ s/\.git//r, uri_escape($target_branch), uri_escape(current_branch());
    say "Open: $url";
    system open => $url if $^O eq 'darwin';
}

__DATA__
github.com
